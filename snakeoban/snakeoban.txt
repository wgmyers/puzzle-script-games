title Snakeoban
author Wayne Myers
homepage www.conniptions.org

require_player_movement

========
OBJECTS
========

Background
Black DarkBlue
00000
00000
00100
00000
00000


Target
Blue
0...0
.0.0.
..0..
.0.0.
0...0

Wall
Grey DarkGrey
00010
11111
01000
11111
00010

Player
Green Black Red Lightgrey Darkgreen
.444.
01010
00200
03230
.2.2.

PlayerBody
Green Darkgreen Lightgreen
.111.
00020
00000
00000
.000.

PlayerTail
Green Black Darkgreen Lightgreen
.222.
00030
00000
00100
.010.

Crate1
Brown Orange Yellow
10000
01220
02120
02210
00001

Crate2
Brown Blue Yellow
10000
01220
02120
02210
00001

Apple
Red Green LightRed
..1..
.000.
00020
00000
.000.


UpMarker
RED
..0..
.000.
0.0.0
..0..
..0..

LeftMarker
RED
..0..
.0...
00000
.0...
..0..

RightMarker
RED
..0..
...0.
00000
...0.
..0..

DownMarker
RED
..0..
..0..
0.0.0
.000.
..0..

=======
LEGEND
=======

. = Background
# = Wall
@ = Player
> = PlayerBody and RightMarker
R = PlayerTail and RightMarker
< = PlayerBody and LeftMarker
L = PlayerTail and LeftMarker
^ = PlayerBody and UpMarker
U = PlayerTail and UpMarker
J = PlayerBody and DownMarker
D = PlayerTail and DownMarker
C = Crate1
O = Crate2 and Target
X = Target
A = Apple

Marker = UpMarker or LeftMarker or RightMarker or DownMarker
PlayerPart = Player or PlayerBody or PlayerTail
Crate = Crate1 or Crate2

=======
SOUNDS
=======

Undo 50721302
Restart 31950301
Player MOVE 71013707
Crate MOVE 30715307
SFX0 88208701  (Eat apple)
SFX1 72499300  (Crate on target)
SFX2 90267708  (Win, but where to bind it?)

================
COLLISIONLAYERS
================

Marker,
Background,
Target
Player, PlayerBody, PlayerTail, Wall, Crate, Apple


======
RULES
======

[ > Player | Crate ] -> [ > Player | > Crate ]

left [ > Player | Apple | ] -> [ PlayerBody Leftmarker | Player | SFX0]
right [ > Player | Apple | ] -> [ PlayerBody Rightmarker | Player | SFX0 ]
up [ > Player | Apple | ] -> [ PlayerBody Upmarker | Player | SFX0 ]
down [ > Player | Apple | ] -> [ PlayerBody Downmarker | Player | SFX0 ]

[ left Player ] -> [ left Player Leftmarker ]
[ right Player ] -> [ right Player Rightmarker ]
[ up Player ] -> [ up Player Upmarker ]
[ down Player ] -> [ down Player Downmarker ]

startloop
late left [ PlayerBody Leftmarker | No PlayerPart ] -> [ Leftmarker | PlayerBody ]
late right [ PlayerBody Rightmarker | No PlayerPart ] -> [ Rightmarker | PlayerBody ]
late up [ PlayerBody Upmarker | No PlayerPart ] -> [ Upmarker | PlayerBody ]
late down [ PlayerBody Downmarker | No PlayerPart ] -> [ Downmarker | PlayerBody ]
endloop

late left [ PlayerTail Leftmarker | No PlayerPart ] -> [ | PlayerTail ]
late right [ PlayerTail Rightmarker | No PlayerPart ] -> [ | PlayerTail ]
late up [ PlayerTail Upmarker | No PlayerPart ] -> [ | PlayerTail ]
late down [ PlayerTail Downmarker | No PlayerPart ] -> [ | PlayerTail ]

late [ Crate1 Target ] -> [ Crate2 Target] SFX1
late [ Crate2 No Target ] -> [ Crate1 ]

==============
WINCONDITIONS
==============

All Crate on Target
No Apple

=======
LEVELS
=======


Message Push The Blocks In The Usual Way

#########
#.......#
#.X.C.X.#
#...##..#
#.CR>@C.#
#..##...#
#.X.C.X.#
#.......#
#########

Message You Must Also Eat The Apples

#########
#.......#
#.A.C.A.#
#..###..#
#.AR>@A.#
#..###..#
#.A.X.A.#
#.......#
#########

Message A Square Meal

#############
#........#..#
#.X......#X.#
###A.CAC....#
#R>@.AAA....#
#....CAC.####
#.X#......X.#
#..#........#
#############


Message Poisoned Apples

###############
#...A......##X#
#.C.#.C..C.#..#
#...#......#..#
##.##.A@<L.##.#
#A.A#.......#.#
#.X.#.........#
#A.A#.....A..X#
###############

Message Apple Free

#######
#..##X#
#.....#
#@#C..#
#^.CDX#
#^<<<.#
#######

Message Crate Free

#######
#AA.AA#
#AADAA#
#AAJAA#
#AA@AA#
#AAAAA#
#######

Message Vaguely Familiar

########
#...#..#
#.A.#CX#
#....CX#
#...#CX#
#.A.#..#
#...#@L#
########

